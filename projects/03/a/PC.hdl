// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Register(in=infinal, load=loadfinal, out=innerOut, out=out);

    // check if load pin should be set
    Or(a=reset, b=inc, out=resetOrInc);
    Or(a=resetOrInc, b=load, out=loadfinal);

    // increment out
    Inc16(in=innerOut, out=incIn);

    // build possible inputs
    Mux16(a[0..15]=false, b=incIn, sel=inc, out=incElseOut);
    Mux16(a=incElseOut, b=in, sel=load, out=loadElseInc);
    Mux16(a=loadElseInc, b[0..15]=false, sel=reset, out=infinal);
}
